asm b=0
; Check for victory against thunderbird.
; If we pick up a key in tbird's room, goto the victory sequence.
;
; I haven't figured out why transferring directly to the victory sequence
; results in a partially corrupted view of the screen.  This hack forces
; a room entry into room 61 (the victory screen) and then triggers the
; victory sequence.

TBIRD_ROOM = 53

bank7_remove_enemy_or_item = $dd47
bank0_Side_View4_walking = $93bc
victory_frame = $7ba

.org $feaa
tbird_victory_check:
    lda     $0707       ; current world
    cmp     #5          ; GP?
    bne     done
    lda     $0561       ; room number
    cmp     #TBIRD_ROOM ; tbird?
    bne     done
    inc     victory_frame
done:
    jmp     bank7_remove_enemy_or_item

; Check for victory after key-pickup.
.org $e7b8
    jmp     tbird_victory_check


; Insert our victory check into link's sideview walking routine
; (becuase it runs every frame)
.org $9381
    jsr victory_check

; The address $bd80 is just a convenient blank space in bank0 which wont
; conflict with the dynamic banks feature of z2edit.
.org $bd80
victory_check:
    lda victory_frame
    cmp #1                      ; frame one, move link
    bne victory_frame2
victory_frame1:
    ; trigger transfer to the victory room
    inc victory_frame           ; incrment victory frame counter
    lda #0
    sta $701                    ; enter from the left
    sta $736                    ; command zero = load room
    sta $75c                    ; enter room 0
    lda #2
    sta $706                    ; overworld 2
    sta $56c                    ; palace code 2
    lda #5
    sta $769                    ; bank 5
    sta $707                    ; world 5
    lda #9
    sta $56b                    ; town code (why we have to set it?)
    lda #54
    sta $748                    ; overworld connector 54 = GP.
    lda #61                     ; room 61 is the victory room
    sta $561
    bne victory_check_done
victory_frame2:
    cmp #2
    bne victory_check_done
    lda #3
    sta $76c                    ; Trigger automatic routine #3 (victory)
victory_check_done:
    jmp bank0_Side_View4_walking

.end
