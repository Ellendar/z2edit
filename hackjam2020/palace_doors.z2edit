######################################################################
# Add the town-door mechanic to palaces.
######################################################################
set ibase 16

# Copy the town top doorframe to all Palace CHR banks.
# commented out since this was already applied and don't want to overwrite.
#charcopy 9:a4 7:49
#charcopy 9:a7 7:4a
#charcopy b:a4 7:49
#charcopy b:a7 7:4a
#charcopy d:a4 7:49
#charcopy d:a7 7:4a
#charcopy 13:a4 7:49
#charcopy 13:a7 7:4a
#charcopy 15:a4 7:49
#charcopy 15:a7 7:4a
#charcopy 17:a4 7:49
#charcopy 17:a7 7:4a
#charcopy 19:a4 7:49
#charcopy 19:a7 7:4a

asm b=4 0
bank7_set_ram_address_for_object = $c944
bank7_place_object_vertical = $df56
bank7_Mute_music_when_loading_between_areas = $d03d

;; Set CHR tile codes for door components.
;; This creates background objects $41 and $51.
;; Object $41 is the top door frame.
;; Object $51 looks just like palace bricks, but is a distinct object
;;            so the door routine can detect when Link stands on it.
.org $8341
.db $a4,$f5,$a7,$f5
.org $8381
.db $64,$65,$65,$64

.org $9ee0
;; Door tiles bottom to top.
door_construction_table:
.db $51,$40,$40,$41

;; Routine to construct the door object.
palace_door_construction_routine:
    ldx #3                                  ; Door is 4 tiles tall.
    stx $0
    jsr bank7_set_ram_address_for_object    ; Location where we'll put it.
loop:
    lda door_construction_table,x           ; Get the object id
    jsr bank7_place_object_vertical         ; Place it
    dex
    dec $0
    bpl loop                                ; Repeat
    rts

;; Take over "small object 6" which is currently a duplicate of the
;; conventional palace locked door.  Patch in our door routine.
.org $813f
.dw palace_door_construction_routine

;; Set the magic door tile in the magic tiles table.
.org $851a
.db $51

.org $d031
;; The vanilla door exit routine would always change the music unless you were
;; in old kasuto.  Change it to always change the music in towns, but not
;; in palaces.
door_exit_done = $d041
door_exit_music:
    lda $0707                               ; Current world
    cmp #3                                  ; worlds >= 3 are palaces
    bcs door_exit_done                      ; branch greater-or-equal

;; In freespace, after the softlock fix:
.org $feb3
maybe_mute_music_between_areas:
    lda $0707                               ; current world
    cmp #3                                  ; worlds >= 3 are palaces
    bcs dont_mute_music
    jmp bank7_Mute_music_when_loading_between_areas ; mute music and return
dont_mute_music:
    pla                                     ; pop the return address
    pla                                     ; cuz we aren't going back there
    jmp $cfec                               ; goto eval room entry conditions

;; Patch it into the room exit routine
.org $cfe5
    jsr maybe_mute_music_between_areas
.end

